FUNCTION_BLOCK alignment

VAR_INPUT
        dist : REAL; // distance (% of perception radius)
        hDiff : REAL; // heading difference (in degrees)
END_VAR

VAR_OUTPUT
        hChg : REAL; // heading change (in degrees)
END_VAR

FUZZIFY dist
        TERM tooClose := (0, 1) (80, 0);
        TERM good := (0, 0) (60, 1) (120, 0);
        TERM tooFar := (40, 0) (100, 1);
END_FUZZIFY

FUZZIFY hDiff
        TERM left := (-180, 1) (0, 0);
        TERM none := (-180, 0) (0, 1) (180, 0);
        TERM right := (0, 0) (180, 1);
END_FUZZIFY

DEFUZZIFY hChg
        TERM left := (-360, 0) (-180, 1) (0, 0);
        TERM none := (-180, 0) (0, 1) (180, 0);
        TERM right := (0, 0) (180, 1) (360, 0);
        METHOD : COG; // LM, RM, MM
        ACCU : MAX;
        DEFAULT := 0;
        RANGE := (-360 .. 360); // TODO: check if (-180 .. 180) changes results
END_DEFUZZIFY

RULEBLOCK alignment
        AND : PROD; // MIN
		OR : MAX;
        ACT : MIN;
        RULE 1 : if (dist is tooClose) then hChg is none;
        RULE 2 : if (dist is tooFar) then hChg is none;
        RULE 3 : if (dist is good) and (hDiff is none) then hChg is none;
        RULE 4 : if (dist is good) and (hDiff is left) then hChg is left;
        RULE 5 : if (dist is good) and (hDiff is right) then hChg is right;
END_RULEBLOCK

END_FUNCTION_BLOCK