FUNCTION_BLOCK cohesion

VAR_INPUT
        dist : REAL; // distance (% of perception radius)
        pDiff : REAL; // position difference (in degrees)
END_VAR

VAR_OUTPUT
        hChg : REAL; // heading change (in degrees)
END_VAR

FUZZIFY dist
        TERM closeEnough := (0, 1) (40, 1) (100, 0);
        TERM tooFar := (0, 0) (40, 0) (100, 1);
END_FUZZIFY

FUZZIFY pDiff
        TERM left := (-180, 1) (0, 0); // left or behind
        TERM none := Triangle -180 0 180; // in front
        TERM right := (0, 0) (180, 1); // right or behind
END_FUZZIFY

DEFUZZIFY hChg
        TERM left := Triangle -360 -180 0;
        TERM none := Triangle -180 0 180;
        TERM right := Triangle 0 180 360;
        METHOD : COG; // LM, RM, MM
        ACCU : MAX;
        DEFAULT := 0;
        RANGE := (-360 .. 360); // TODO: check if (-180 .. 180) changes results
END_DEFUZZIFY

RULEBLOCK cohesion
        AND : PROD; // MIN
		OR : MAX;
        ACT : MIN;
        RULE 1 : if (dist is tooFar) then hChg is none; // TODO: test removing
        RULE 2 : if (dist is closeEnough) then hChg is none;
        RULE 3 : if (dist is tooFar) and (pDiff is none) then hChg is none;
        RULE 4 : if (dist is tooFar) and (pDiff is left) then hChg is left;
        RULE 5 : if (dist is tooFar) and (pDiff is right) then hChg is right;
END_RULEBLOCK

END_FUNCTION_BLOCK