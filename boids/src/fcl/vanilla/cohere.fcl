FUNCTION_BLOCK cohesion

VAR_INPUT
    distance : REAL; // distance (% of perception radius)
    position : REAL; // perceived neighbor's angular offset (in degrees)
END_VAR

VAR_OUTPUT
    headingChange : REAL; // flight direction change (in degrees)
END_VAR

FUZZIFY distance
    //TERM closeEnough := (0, 1) (40, 1) (100, 0);
    //TERM tooFar      := (0, 0) (40, 0) (100, 1);
    TERM closeEnough := (0, 1) (60, 0) (100, 0);
    TERM tooFar      := (0, 0) (60, 1) (100, 1);
END_FUZZIFY

FUZZIFY position
    TERM front := trian -180 0 180;
    TERM leftOrBehind  := (-180, 1) (0, 0) (180, 0);
    TERM rightOrBehind := (-180, 0) (0, 0) (180, 1);
END_FUZZIFY

DEFUZZIFY headingChange
	TERM none      := trian -180    0 180;
	TERM turnLeft  := trian -360 -180   0;
	TERM turnRight := trian    0  180 360;
	METHOD  : COG;
	DEFAULT	:= 0;
END_DEFUZZIFY

RULEBLOCK cohesion
	AND	 : MIN; // PROD
	OR   : MAX;
 	ACT	 : MIN;
	ACCU : MAX;
	RULE 1 : IF position IS front THEN headingChange IS none;
	RULE 2 : IF position IS leftOrBehind THEN headingChange IS turnLeft;
	RULE 3 : IF position IS rightOrBehind THEN headingChange IS turnRight;
END_RULEBLOCK

END_FUNCTION_BLOCK