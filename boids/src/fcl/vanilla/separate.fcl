FUNCTION_BLOCK separation

VAR_INPUT
    distance : REAL; // distance (% of separation radius)
    position : REAL; // perceived neighbor's angular offset (in degrees)
END_VAR

VAR_OUTPUT
    headingChange : REAL; // flight direction change (in degrees)
END_VAR

FUZZIFY distance
    TERM tooClose  := (0, 1) (80, 0) (100, 0);
    TERM farEnough := (0, 0) (50, 1) (100, 1);
END_FUZZIFY

FUZZIFY position
    TERM behind       := (-180, 1) (0, 0) (180, 1);
    TERM leftOrFront  := (-180, 0) (0, 1) (  1, 0);
    TERM rightOrFront := (   -1, 0) (0, 1) (180, 0);
END_FUZZIFY

DEFUZZIFY headingChange
	TERM none      := trian -180    0 180;
	TERM turnLeft  := trian -360 -180   0;
	TERM turnRight := trian    0  180 360;
	METHOD  : COG;
	DEFAULT	:= 0;
END_DEFUZZIFY

RULEBLOCK separation
	AND	 : MIN; // PROD
	OR   : MAX;
 	ACT	 : MIN;
	ACCU : MAX;
	RULE 1 : IF position IS behind THEN headingChange IS none;
	RULE 2 : IF position IS leftOrFront THEN headingChange IS turnRight;
	RULE 3 : IF position IS rightOrFront THEN headingChange IS turnLeft;
END_RULEBLOCK

END_FUNCTION_BLOCK